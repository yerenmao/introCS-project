class Bullet {
    field int x, y, size, d;
    field ListManager listman;

    constructor Bullet new(int a, int b, int dir, ListManager listmanager) {
        let x = a; let y = b; let d = dir;
        let size = 9;
        let listman = listmanager;
        return this;
    }
    method void free() {
        do render(false);
        do Memory.deAlloc(this);
        return;
    }

    method boolean move() {
        if((y=1) | (x=1) | (y+size+1=256) | (x+size+1=512)) {
            do free();
            return true;
        }
        if(listman.hittrap(x, y, size)) {
            do free();
            return true;
        }
        do render(false);
        if((d = 0) | (d = 1) | (d = 7)) {let y = y - 1;}
        if((d = 1) | (d = 2) | (d = 3)) {let x = x + 1;}
        if((d = 3) | (d = 4) | (d = 5)) {let y = y + 1;}
        if((d = 5) | (d = 6) | (d = 7)) {let x = x - 1;}
        do render(true);
        return false;
    }

    method void render(boolean bool) {
        do Screen.setColor(bool);
        do Screen.drawRectangle(x, y+3, x, y+6);
        do Screen.drawRectangle(x+9, y+3, x+9, y+6);
        do Screen.drawRectangle(x+3, y, x+6, y);
        do Screen.drawRectangle(x+3, y+9, x+6, y+9);
        do Screen.drawRectangle(x+1, y+1, x+8, y+8);
        return;
    }
}