class EnemyBullet {
    field int x, y, size, d;
    field ListManager listman;
    field EnemyBullet prev, next;

    constructor EnemyBullet new(int a, int b, int dir, ListManager listmanager) {
        let x = a; let y = b; let d = dir;
        let size = 9;
        let prev = null; let next = null;
        let listman = listmanager;
        return this;
    }
    method void free() {
        do render(false);
        do Memory.deAlloc(this);
        return;
    }

    method void tick() {
        do move();
        return;
    }

    method void move() {
        if((y=1) | (x=1) | (y+size+1=256) | (x+size+1=512)) {
            do listman.removeenemybullet(this);
            do free();
            return;
        }
        do render(false);
        if((d = 0) | (d = 1) | (d = 7)) {let y = y - 1;}
        if((d = 1) | (d = 2) | (d = 3)) {let x = x + 1;}
        if((d = 3) | (d = 4) | (d = 5)) {let y = y + 1;}
        if((d = 5) | (d = 6) | (d = 7)) {let x = x - 1;}
        do render(true);
        return;
    }

    method boolean hitted(int bx, int by, int bs) {
        if(Tools.collide(x, y, size, size, bx, by, bs, bs)) {
            return true;
        }
        return false;
    }

    method void render(boolean bool) {
        do Screen.setColor(bool);
        do Screen.drawRectangle(x, y, x+size, y);
        do Screen.drawRectangle(x, y+1, x+size, y+1);
        do Screen.drawRectangle(x, y+size-1, x+size, y+size-1);
        do Screen.drawRectangle(x, y+size, x+size, y+size);
        do Screen.drawRectangle(x, y+2, x, y+size-2);
        do Screen.drawRectangle(x+1, y+2, x+1, y+size-2);
        do Screen.drawRectangle(x+size-1, y+2, x+size-1, y+size-2);
        do Screen.drawRectangle(x+size, y+2, x+size, y+size-2);
        return;
    }

    method EnemyBullet getprev() {return prev;}
    method EnemyBullet getnext() {return next;}
    method void setprev(EnemyBullet eb) {let prev = eb; return;}
    method void setnext(EnemyBullet eb) {let next = eb; return;}
}